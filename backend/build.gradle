import org.springframework.boot.gradle.plugin.SpringBootPlugin

// https://stackoverflow.com/questions/56338999/how-to-avoid-repeating-dependency-versions-in-a-multi-module-gradle-project
// https://www.techscore.com/blog/2019/02/28/springboot%E3%82%A2%E3%83%97%E3%83%AA%E3%82%92kubernetes%E3%81%A7%E4%BD%9C%E3%81%A3%E3%81%A6%E3%81%BF%E3%81%9F/


buildscript {
    repositories {
        mavenCentral()
        maven {
           url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:4.1.6"
    }
}

plugins {
    id "org.springframework.boot" version "2.2.3.RELEASE" apply false
}

allprojects {
    group = 'com.github.orimajp'
    version = '0.0.1-SNAPSHOT'
}

subprojects {
    apply plugin: "java"
    apply plugin: "idea"
    apply plugin: "eclipse"
    apply plugin: "io.freefair.lombok"
    apply plugin: 'io.spring.dependency-management'
    sourceCompatibility = '11'
    repositories {
        mavenCentral()
    }
    idea {
        module {
            downloadJavadoc = true
            downloadSources = true

            inheritOutputDirs = false
            outputDir = compileJava.destinationDir
            testOutputDir = compileTestJava.destinationDir
        }
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Greenwich.RELEASE'
        }
    }

    dependencies {
        implementation 'com.github.rozidan:modelmapper-spring-boot-starter:2.3.1'
    }
}
